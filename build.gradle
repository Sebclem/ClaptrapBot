plugins {
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'groovy'
}


def versionObj = new Version(major: 0, minor: 2, revision: 0)

group = "net.broken"
archivesBaseName = "ClaptrapBot"
version = "$versionObj"

sourceCompatibility = '17'


repositories {
    mavenCentral()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
}
jar {
    enabled(false)
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    implementation("org.springframework.boot:spring-boot-starter-log4j2")
    implementation 'org.codehaus.groovy:groovy-all:3.0.8'


    implementation 'com.sedmelluq:lavaplayer:1.3.77'
    implementation 'net.dv8tion:JDA:4.4.0_350'
    implementation group: 'org.json', name: 'json', version: '20210307'
    implementation 'org.springframework.security:spring-security-web:5.5.0'
//     JPA Data (We are going to use Repositories, Entities, Hibernate, etc...)
    implementation("org.springframework.boot:spring-boot-starter-data-jpa") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    implementation(platform("org.apache.logging.log4j:log4j-bom:2.17.1"))
//     Use MySQL Connector-J
    implementation 'mysql:mysql-connector-java'
    implementation 'org.reflections:reflections:0.9.12'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.api-client:google-api-client:1.31.5'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20210410-1.31.0'


    implementation group: 'org.jsoup', name: 'jsoup', version: '1.15.1'


    implementation("org.springframework.boot:spring-boot-starter-thymeleaf") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

class Version {
    String major, minor, revision

    static String getBuild() {
        System.getenv("GITHUB_RUN_NUMBER") ?: System.getProperty("BUILD_NUMBER") ?:
                System.getenv("GIT_COMMIT")?.substring(0, 7) ?: System.getProperty("GIT_COMMIT")?.substring(0, 7) ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}
